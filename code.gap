###
# 
##  This code for GAP ( version 4.13.0 of 2024-03-15 ) accompanies the paper
#  
##  Conjugate generation of sporadic almost simple groups
# 
##  by Danila O. Revin and Andrei V. Zavarnitsine
#
##  Date: April 15, 2024


##  Every section of the following code can be copy-pasted into a working GAP session
##  A command output is given after a single '#' 
##  A comment is given after a double '#' 

##  This section relates to proof of Lemma 3(i)
#
##  We check that m(nX,nX,pA)>0 for the almost simple group G=Aut(S),
##  where S is a sporadic group such that the triple (S,nX,p) is one of 
#
## ( M_12, 2C, 11 ), ( M_22, 2C, 11 ), ( J_2, 2C, 7 ), ( J_3, 2B, 19 ),
## ( O'N, 2B, 31), ( HS, 2D, 11 ), ( HN, 2C, 19 ).
#

C:=CharacterTable( "M12.2" );;
ClassMultiplicationCoefficient( C, C.2c, C.2c, C.11a ); # 11

C:=CharacterTable( "M22.2" );;
ClassMultiplicationCoefficient( C, C.2c, C.2c, C.11a ); # 11

C:=CharacterTable( "J2.2" );;
ClassMultiplicationCoefficient( C, C.2c, C.2c, C.7a );  # 7

C:=CharacterTable( "J3.2" );;
ClassMultiplicationCoefficient( C, C.2b, C.2b, C.19a ); # 19

C:=CharacterTable( "ON.2" );;
ClassMultiplicationCoefficient( C, C.2b, C.2b, C.31a ); # 31

C:=CharacterTable( "HS.2" );;
ClassMultiplicationCoefficient( C, C.2d, C.2d, C.11a ); # 11

C:=CharacterTable( "HN.2" );;
ClassMultiplicationCoefficient( C, C.2c, C.2c, C.19a ); # 19

#
##




##  This section relates to the proof of main result, case (S, nX) = (Suz, 3A)
#
##  We check that every pair of elements in the conjugacy class 3A of the sporadic group Suz
##  generates one of the following groups:
#
##  C_3, C_3 x C_3, A_4, A_5, SL(2,3)
#

Suz := AtlasGroup( "Suz" );;                ##  Suz constructed from the online Atlas data
Generators := GeneratorsOfGroup( Suz );;    ##  Standard generators of Suz
a := Generators[1];; b := Generators[2];;   ##  a is in class 2B, b is in class 3B, 
                                          
List( [ a, b, a*b, (a*b)^2*b ], Order );
#  [ 2, 3, 13, 15 ]  ##  ab has order 13 and ababb has order 15.

t := ((a*b^2)^3*a*b)^7;;  ## t is a representative of class 3A
Order( t ); # 3

Centr_t := Centralizer( Suz, t );; ## The Centralizer of t in Suz
Size( Centr_t ); # 9797760         ## The order of the centralizer is 9797760
                                   ## which confirms that t is in 3A

## The obits of Suz on pairs of elements in class 3A are
## in a bijection with the orbits  Cent_t on 3A

ConjClass_t := ConjugacyClass( Suz, t );; ## The conjugacy class of t in Suz ( which is 3A ) 

Orbits_t := OrbitsDomain( Centr_t, ConjClass_t );; ## The orbits of the centralizer of t on 3A

List( Orbits_t, Size );
# [ 27216, 486, 8505, 8505, 486, 560, 1, 1 ] ## There are 8 orbits with these sizes

Representatives := List( Orbits_t, Representative );; ## A list of orbit representatives 

## Checking that distinct orbits of the same size contain mutual inverses:

Representatives[2]^-1 in Orbits_t[5]; # true
Representatives[3]^-1 in Orbits_t[4]; # true
Representatives[7]^-1 in Orbits_t[8]; # true

## Thus, we have the following five cases of subgroups
## generated by a pair of elements in class 3A of Suz:

IdGroup( Group( t, Representatives[1] ) ); # [ 60, 5 ]   ##  A5
IdGroup( Group( t, Representatives[2] ) ); # [ 12, 3 ]   ##  A4
IdGroup( Group( t, Representatives[3] ) ); # [ 24, 3 ]   ##  SL(2,3)
IdGroup( Group( t, Representatives[6] ) ); # [ 9, 2 ]    ##  C3 x C3
IdGroup( Group( t, Representatives[7] ) ); # [ 3, 1 ]    ##  C3

#
##
### 
